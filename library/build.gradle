/*
 * Copyright 2018 Wultra s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace "com.wultra.android.sslpinning"
    testNamespace "com.wultra.android.sslpinning.test"
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    // avoids a gradle warning, otherwise unused due to custom config in android-release-aar.gradle
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    lintOptions {
        // to handle warning coming from a transitive dependency
        // - obsolete 'androidx.fragment' through 'powerauth-sdk'
        disable 'ObsoleteLintCustomCheck'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly "com.wultra.android.powerauth:powerauth-sdk:${powerAuthSdkVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "androidx.annotation:annotation:1.7.0"

    testImplementation "com.wultra.android.powerauth:powerauth-sdk:${powerAuthSdkVersion}"
    testImplementation 'junit:junit:4.13.2'
    testImplementation("io.mockk:mockk:1.13.5")
    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    testImplementation "io.getlime.security:powerauth-java-crypto:1.4.0"

//    androidTestImplementation 'androidx.core:core-ktx:+'
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "com.wultra.android.powerauth:powerauth-sdk:${powerAuthSdkVersion}"
    androidTestImplementation 'com.squareup.okhttp3:okhttp:4.10.0'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion") {
            because "Avoids conflicts with 'kotlin-stdlib'"
        }
    }
}

apply from: 'android-release-aar.gradle'
apply from: 'android-coverage.gradle'